#include "headers/sfall/sfall.h"

#include "headers/fallout-2-adjustments.h"

variable
  priceCap,
  debuggingEnabled;

procedure barterPriceHandler;

procedure start begin
  if game_loaded then begin
    priceCap = atof(get_ini_string(INI_PATH "|priceCap|priceCap"));

    // If disabled in settings, do not do anything else.
    if priceCap == 0 orElse priceCap == -1 then return;

    debuggingEnabled = get_ini_setting(INI_PATH "|debugging|priceCap");

    register_hook_proc(HOOK_BARTERPRICE, barterPriceHandler);
  end
end

procedure barterPriceHandler begin
  variable
    offeredPrice = get_sfall_arg_at(2),
    offeredPriceUnmodified = get_sfall_arg_at(5),
    tradingWithPartyMember = get_sfall_arg_at(9),
    minPrice = floor(offeredPriceUnmodified * priceCap);

  if debuggingEnabled == 1 then begin
    display_msg("offeredPrice: " + offeredPrice);
    display_msg("offeredPriceUnmodified: " + offeredPriceUnmodified);
    display_msg("tradingWithPartyMember: " + tradingWithPartyMember);
    display_msg("priceCap: " + priceCap);
    display_msg("minPrice: " + minPrice);
  end

  if tradingWithPartyMember then return;

  if offeredPrice < minPrice then set_sfall_return(minPrice);
end
