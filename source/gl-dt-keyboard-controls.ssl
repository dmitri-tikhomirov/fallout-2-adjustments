#include "headers/sfall/sfall.h"
#include "headers/fallout-2-adjustments.h"

variable
  keyMapping,
  disabledKeys,
  openCloseWithSameKey,
  needToSetReleased;

procedure keyPressHandler;
procedure getKeyCode(variable key);

procedure start begin
  if game_loaded then begin
    if get_ini_setting(INI_PATH "|keyboardControls|customControlsEnabled") == 1
        then begin
      keyMapping = get_ini_section(INI_PATH, "keyboardControlsMapping");

      variable index, value;

      // Convert key names to codes.
      foreach index: value in keyMapping begin
        index = getKeyCode(index);

        keyMapping[index] = getKeyCode(value);

        // display_msg(index + "=" + keyMapping[index]);
      end

      fix_array(keyMapping);

      disabledKeys = string_split(
        get_ini_string(INI_PATH "|keyboardControls|disabledKeys"), ",");

      // Convert key names to codes.
      foreach index: value in disabledKeys begin
        disabledKeys[index] = getKeyCode(value);

        // display_msg("disabledKeys[" + index + "]: " + disabledKeys[index]);
      end

      fix_array(disabledKeys);

      openCloseWithSameKey =
        get_ini_setting(INI_PATH "|keyboardControls|openCloseWithSameKey");

      register_hook_proc(HOOK_KEYPRESS, keyPressHandler);
    end
  end
end

procedure keyPressHandler begin
  variable
    pressedNotReleased = get_sfall_arg_at(0),
    pressedKey = get_sfall_arg_at(1),
    mappedDefaultKey = scan_array(keyMapping, pressedKey);

  // display_msg("pressedNotReleased: " + pressedNotReleased);
  // display_msg("pressedKey: " + pressedKey);
  // display_msg("mappedDefaultKey: " + mappedDefaultKey);
  // display_msg("Inventory mode: " + (get_game_mode bwand INVENTORY));
  // display_msg("Skilldex mode: " + (get_game_mode bwand SKILLDEX));

  if openCloseWithSameKey == 1 then begin
    // Close Inventory window with the same key.
    // Game mode changes before you release the key,
    // but you need to set both to override correctly.
    if (get_game_mode bwand INVENTORY andAlso pressedNotReleased orElse
        needToSetReleased) andAlso
        (mappedDefaultKey == 23 orElse pressedKey == 23) then begin
      if pressedNotReleased then needToSetReleased = true;
        else needToSetReleased = false;

      set_sfall_return(getKeyCode("Esc"));
    end

    // Close Skilldex window with the same key.
    if (get_game_mode bwand SKILLDEX andAlso pressedNotReleased orElse
        needToSetReleased) andAlso
        (mappedDefaultKey == 31 orElse pressedKey == 31) then begin
      if pressedNotReleased then needToSetReleased = true;
        else needToSetReleased = false;

      set_sfall_return(getKeyCode("Esc"));
    end
  end

  if mappedDefaultKey != -1 then set_sfall_return(mappedDefaultKey);

  if scan_array(disabledKeys, pressedKey) != -1 then
    set_sfall_return(getKeyCode("DISABLED"));
end

// DirectX scancodes (DX codes) for input keys.
procedure getKeyCode(variable key) begin
  variable keyCodes = {
    "ESC":          1,
    "ESCAPE":       1,
    "1":            2,
    "2":            3,
    "3":            4,
    "4":            5,
    "5":            6,
    "6":            7,
    "7":            8,
    "8":            9,
    "9":            10,
    "0":            11,
    "-":            12,
    "MINUS":        12,
    "HYPHEN":       12,
    "=":            13,
    "+":            13,
    "EQUALS":       13,
    "BACKSPACE":    14,
    "TAB":          15,
    "Q":            16,
    "W":            17,
    "E":            18,
    "R":            19,
    "T":            20,
    "Y":            21,
    "U":            22,
    "I":            23,
    "O":            24,
    "P":            25,
    "[":            26,
    "{":            26,
    "BRACKET":      26,
    "LEFTBRACKET":  26,
    "LBRACKET":     26,
    "]":            27,
    "}":            27,
    "RIGHTBRACKET": 27,
    "RBRACKET":     27,
    "ENTER":        28,
    "RETURN":       28,
    "CTRL":         29,
    "LEFTCTRL":     29,
    "LCTRL":        29,
    "CONTROL":      29,
    "LEFTCONTROL":  29,
    "LCONTROL":     29,
    "A":            30,
    "S":            31,
    "D":            32,
    "F":            33,
    "G":            34,
    "H":            35,
    "J":            36,
    "K":            37,
    "L":            38,
    ";":            39,
    "SEMICOLON":    39,
    "'":            40,
    "APOSTROPHE":   40,
    "`":            41,
    "~":            41,
    "BACKTICK":     41,
    "TILDE":        41,
    "SHIFT":        42,
    "LEFTSHIFT":    42,
    "LSHIFT":       42,
    "\\":           43,
    "|":            43,
    "BACKSLASH":    43,
    "Z":            44,
    "X":            45,
    "C":            46,
    "V":            47,
    "B":            48,
    "N":            49,
    "M":            50,
    ",":            51,
    "<":            51,
    "COMMA":        51,
    ".":            52,
    ">":            52,
    "PERIOD":       52,
    "/":            53,
    "?":            53,
    "SLASH":        53,
    "FORWARDSLASH": 53,
    "RIGHTSHIFT":   54,
    "RSHIFT":       54,
    "*":            55,
    "NUMPAD*":      55,
    "NUM*":         55,
    "ALT":          56,
    "LEFTALT":      56,
    "LALT":         56,
    "SPACE":        57,
    "SPACEBAR":     57,
    "CAPSLOCK":     58,
    "CAPS":         58,
    "CAPITAL":      58,
    "F1":           59,
    "F2":           60,
    "F3":           61,
    "F4":           62,
    "F5":           63,
    "F6":           64,
    "F7":           65,
    "F8":           66,
    "F9":           67,
    "F10":          68,
    "NUMLOCK":      69,
    "NUM":          69,
    "SCROLLOCK":    70,
    "SCROLL":       70,
    "NUMPAD7":      71,
    "NUM7":         71,
    "NUMPAD8":      72,
    "NUM8":         72,
    "NUMPAD9":      73,
    "NUM9":         73,
    "NUMPAD-":      74,
    "NUM-":         74,
    "NUMPAD4":      75,
    "NUM4":         75,
    "NUMPAD5":      76,
    "NUM5":         76,
    "NUMPAD6":      77,
    "NUM6":         77,
    "NUMPAD+":      78,
    "NUM+":         78,
    "NUMPAD1":      79,
    "NUM1":         79,
    "NUMPAD2":      80,
    "NUM2":         80,
    "NUMPAD3":      81,
    "NUM3":         81,
    "NUMPAD0":      82,
    "NUM0":         82,
    "NUMPAD.":      83,
    "NUM.":         83,
    "NUMPAD,":      83,
    "NUM,":         83,
    "DECIMAL":      83,
    "F11":          87,
    "F12":          88,
    "NUMPADENTER":  156,
    "NUMENTER":     156,
    "RIGHTCTRL": 	  157,
    "RCTRL": 	      157,
    "RIGHTCONTROL": 157,
    "RCONTROL": 	  157,
    "NUMPAD/":     	181,
    "NUM/":        	181,
    "RIGHTALT": 	  184,
    "RALT": 	      184,
    "HOME":         199,
    "UP":           200,
    "UPARROW":      200,
    "PAGEUP":       201,
    "PGUP":         201,
    "LEFT":         203,
    "LEFTARROW":    203,
    "RIGHT":        205,
    "RIGHTARROW":   205,
    "END":          207,
    "DOWN":         208,
    "DOWNARROW":    208,
    "PAGEDOWN":     209,
    "PGDN":         209,
    "INSERT":       210,
    "INS":          210,
    "DELETE":       211,
    "DEL":          211,

    // A code to disable keys.
    "DISABLED":     256
  };

  key = string_toupper(key);

  return keyCodes[key];
end
