#include "headers/sfall/sfall.h"
#include "headers/fallout-2-adjustments.h"

procedure start begin
  if game_loaded then begin
    variable
      compareOnly =
        get_ini_setting(INI_PATH "|settingsTransfer|compareOnly"),
      compareAndTransfer =
        get_ini_setting(INI_PATH "|settingsTransfer|compareAndTransfer");

    if (compareOnly == 0 orElse compareOnly == -1) andAlso
        (compareAndTransfer == 0 orElse compareAndTransfer == -1) then
      return;
    else begin
      display_msg("Checking settings...");

      if compareAndTransfer == 1 then display_msg("Transfer is on.");
      else display_msg("Transfer is off.");
    end

    variable
      oldIniPath = get_ini_string(INI_PATH "|settingsTransfer|oldIniPath"),
      newIniPath = get_ini_string(INI_PATH "|settingsTransfer|newIniPath"),
      excludedSettings = string_split(
        get_ini_string(INI_PATH "|settingsTransfer|excludedSettings"), ","),
      oldIniConfig = get_ini_config(oldIniPath),
      section, settings,
      setting, value,
      checkedCount = 0, missingCount = 0, transferredCount = 0;

    // foreach setting in excludedSettings
    //   display_msg(setting + " is excluded.");

    foreach section: settings in oldIniConfig
      foreach setting: value in settings begin
        variable
          settingNewIniFullPath = newIniPath + "|" + section + "|" + setting;

        if get_ini_setting(settingNewIniFullPath) == -1 andAlso
            get_ini_string(settingNewIniFullPath) != "-1" then begin
          missingCount++;

          display_msg(setting + " is missing.");
        end
        else
          if compareAndTransfer == 1 andAlso
              scan_array(excludedSettings, setting) == -1 then begin
            set_ini_setting(settingNewIniFullPath, value);

            transferredCount++;
          end

        checkedCount++;
      end

    display_msg(checkedCount + " setting(s) checked.");
    display_msg(missingCount + " setting(s) missing.");
    display_msg(transferredCount + " setting(s) transferred.");
    display_msg(
      len_array(excludedSettings) + " setting(s) in exclusion list.");

    display_msg("Done.");
  end
end
